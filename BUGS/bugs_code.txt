# BUGS code for mixed trial design
# network meta-analysis
# original code from Au (2022)

model {

	# Overall level with all studies
	# random effects in combining studies from different study designs
	
	for (s in 1:4) {	# by design
	  for (i in nt_cumul[s]:(nt_cumul[s+1] - 1)) {
	  
  	  # no effect in reference first treatment
  	  d_design[ts[i], s] <- step(ts[i] - 1.5) * z[ts[i], s]
  	  z[ts[i], s] ~ dnorm(d[ts[i]], tauALL)
	  }
	}

  for (k in 2:ntALL) {
	  d[k] ~ dnorm(0.1, 0.001)
  }
  
  d[1] <- 0

  sdALL ~ dunif(0, 10)  # prior for overall heterogeneity
  tauALL <- pow(sdALL, -2)

  for (c in 1:(ntALL-1)) {  # pairwise comparisons
	  for (k in (c+1):ntALL) {
	  	or[c,k] <- exp(d[k] - d[c])
	  	lor[c,k] <- d[k] - d[c]
	  }
  }

  rk_bad <- rank(d[])    # assume events are "bad"
  rk_good <- rank(-d[])  # assume events are "good"

  for (k in 1:ntALL) {
  	rk[k] <- rk_good[k]*equals(0, 1) + rk_bad[k] * equals(0, 0)
  }

  for (k in 1:ntALL) {
  	for (j in 1:ntALL) {
  		effectiveness[k,j] <- equals(rk[k],j)
  	}
  }

  for (k in 1:ntALL) {
  	for (j in 1:ntALL) {
  		cumeffectiveness[k,j] <- sum(effectiveness[k, 1:j])
  	}
  }

  for (k in 1:ntALL) {
	  SUCRA[k] <- sum(cumeffectiveness[k, 1:(ntALL-1)])/(ntALL-1)
  }

  # design-level, RCTs
  for (i in offset[1]:(offset[2]-1)) {  # loop through studies
  	wRCT[i,1] <- 0
  	deltaRCT[i, t[i,1]] <- 0 
  	muRCT[i] ~ dnorm(0, 0.001)
  
	  for (k in 1:na[i]) { #loop through arms
	  	r[i,k] ~ dbin(pRCT[i, t[i,k]], n[i,k])
	  	logit(pRCT[i, t[i,k]]) <- muRCT[i] + deltaRCT[i, t[i,k]]  # logistic regression model
	  	rhatRCT[i,k] <- pRCT[i,t[i,k]] * n[i,k]
	  	
	  	# deviance
	  	dev[i,k] <- 2 * (r[i,k] * (log(r[i,k])-log(rhatRCT[i,k])) +
	  			        (n[i,k] - r[i,k]) * (log(n[i,k] - r[i,k]) - log(n[i,k] - rhatRCT[i,k])))
	  } 

    resdev[i] <- sum(dev[i, 1:na[i]])  # sum of deviance

    for (k in 2:na[i]) {  # loop through treatments
    	# Distribution of log odds ratio
    	deltaRCT[i, t[i,k]] ~ dnorm(mdRCT[i, t[i,k]], taudRCT[i, t[i,k]])
    	mdRCT[i, t[i,k]] <- d_design[t[i,k], 1] - d_design[t[i,1], 1] + swRCT[i,k]  # arm correction
    	taudRCT[i, t[i,k]] <- tauRCT *2*(k-1)/k
	
    	# multi-arm adjustment
    	wRCT[i,k] <- deltaRCT[i, t[i,k]] - d_design[t[i,k], 1] + d_design[t[i,1], 1]
    	swRCT[i,k] <- sum(wRCT[i, 2:k-1])/(k-1)
  	}
  }

  sdRCT ~ dunif(0,10)  # prior of heterogeneity
  tauRCT <- pow(sdRCT,-2)
  
  # design-level, case-control studies
  for (i in offset[4]:(offset[5]-1)) {  # loop through studies
  	wCC[i,1] <- 0
  	deltaCC[i, t[i,1]] <- 0 
  	muCC[i] ~ dnorm(0, 0.001)
  
  	for (k in 1:na[i]) {  # loop through arms
  		r[i,k] ~ dbin(pCC[i, t[i,k]], n[i,k])
  		logit(pCC[i, t[i,k]]) <- muCC[i] + deltaCC[i, t[i,k]]  # logistic regression model
  		rhatCC[i,k] <- pCC[i,t[i,k]] * n[i,k]
  		
  		# deviance
  		dev[i,k] <- 2 * (r[i,k] * (log(r[i,k])-log(rhatCC[i,k])) +
  				        (n[i,k] - r[i,k]) * (log(n[i,k]-  r[i,k]) - log(n[i,k] - rhatCC[i,k])))
  	}
  
    resdev[i] <- sum(dev[i, 1:na[i]])  # sum of deviance
  
    for (k in 2:na[i]) {  # loop through treatments
    	# Distribution of log odds ratio
    	deltaCC[i, t[i,k]] ~ dnorm(mdCC[i, t[i,k]], taudCC[i, t[i,k]])
    	mdCC[i, t[i,k]] <- d_design[t[i,k], 4] - d_design[t[i,1], 4] + swCC[i,k]  # arm correction
    	taudCC[i, t[i,k]] <- tauCC *2*(k-1)/k
  	
    	# multi-arm adjustment
    	wCC[i,k] <- deltaCC[i, t[i,k]] - d_design[t[i,k], 4] + d_design[t[i,1], 4]
    	swCC[i,k] <- sum(wCC[i, 2:k-1])/(k-1)
  	}
  }
  
  sdCC ~ dunif(0,10)  # prior of heterogeneity
  tauCC <- pow(sdCC,-2)
  	
  # design-level, retrospective cohort studies
  for (i in offset[2]:(offset[3]-1)) {  # loop through studies
    wRC[i,1] <- 0
    deltaRC[i,t[i,1]] <- 0 
    muRC[i] ~ dnorm(0, 0.001)
    
    for (k in 1:na[i]) {  # loop through arms
      r[i,k] ~ dbin(pRC[i,t[i,k]],n[i,k])
  	  logit(pRC[i, t[i,k]]) <- muRC[i] + deltaRC[i, t[i,k]]  # logistic regression model
      rhatRC[i,k] <- pRC[i, t[i,k]] * n[i,k]
      
      # deviance
      dev[i,k] <- 2 * (r[i,k] * (log(r[i,k]) - log(rhatRC[i,k])) +
                  (n[i,k] - r[i,k]) * (log(n[i,k]-r[i,k]) - log(n[i,k] - rhatRC[i,k])))
  	}
    
    resdev[i] <- sum(dev[i, 1:na[i]])  # sum of deviance
    
    for (k in 2:na[i]) {  # loop through treatments
      # Distribution of log odds ratio
      deltaRC[i, t[i,k]] ~ dnorm(mdRC[i, t[i,k]], taudRC[i, t[i,k]])
      mdRC[i, t[i,k]] <- d_design[t[i,k], 2] - d_design[t[i,1], 2] + swRC[i,k]  # arm correction
      taudRC[i, t[i,k]] <- tauRC *2*(k-1)/k
      
      # multi-arm adjustment
      wRC[i,k] <- deltaRC[i, t[i,k]] - d_design[t[i,k], 2] + d_design[t[i,1], 2]
      swRC[i,k] <- sum(wRC[i, 2:k-1])/(k-1)
    }
  }
  
  sdRC ~ dunif(0,10)    # prior of heterogeneity
  tauRC <- pow(sdRC,-2)
  
  # design-level, prospective cohort studies
  for (i in offset[3]:(offset[4]-1)) {  # loop through studies
    wPC[i,1] <- 0
    deltaPC[i,t[i,1]] <- 0 
    muPC[i] ~ dnorm(0, 0.001)
    
    for (k in 1:na[i]) {  # loop through arms
      r[i,k] ~ dbin(pPC[i, t[i,k]], n[i,k])
    	logit(pPC[i, t[i,k]]) <- muPC[i] + deltaPC[i, t[i,k]]  # logistic regression model
      rhatPC[i,k] <- pPC[i,t[i,k]] * n[i,k]
      
      # deviance
      dev[i,k] <- 2 * (r[i,k] * (log(r[i,k]) - log(rhatPC[i,k])) +
                  (n[i,k] - r[i,k]) * (log(n[i,k] - r[i,k]) - log(n[i,k] - rhatPC[i,k])))
  	}
    
    resdev[i] <- sum(dev[i, 1:na[i]])  # sum of deviance
    
    for (k in 2:na[i]) {  # loop through treatments
      # Distribution of log odds ratio
      deltaPC[i,t[i,k]] ~ dnorm(mdPC[i, t[i,k]], taudPC[i, t[i,k]])
      mdPC[i,t[i,k]] <- d_design[t[i,k], 3] - d_design[t[i,1], 3] + swPC[i,k]  # rm correction
      taudPC[i,t[i,k]] <- tauPC *2*(k-1)/k
      wPC[i,k] <- deltaPC[i, t[i,k]] - d_design[t[i,k], 3] + d_design[t[i,1], 3]
      swPC[i,k] <- sum(wPC[i, 2:k-1])/(k-1)
  	}
  }

  totresdev <- sum(resdev[])  # total deviance

  sdPC ~ dunif(0,10)    # prior of heterogeneity
  tauPC <- pow(sdPC,-2)
}
